plugins {
  id 'base'
}

configurations {
  // Input: The directory that has the Dockerfile and all other
  // artifacts needed at image build time
  appServerDockerDir
  // Input: The directory that has the staged UI assets
  appUIAssetsDir
}

task synthesize {
  description "Builds CloudFormation templates without deploying them"
  inputs.files(fileTree('lib'))
  inputs.files(configurations.appServerDockerDir)
  outputs.files(fileTree('cdk.out'))
  doLast {
    exec {
      workingDir '.'
      commandLine 'cdk', 'synth',
        '--context',
        "server-docker-dir=${configurations.appServerDockerDir.resolve()[0].absolutePath}",
        '--context',
        "ui-assets-dir=${configurations.appUIAssetsDir.resolve()[0].absolutePath}"
    }
  }
}
synthesize.dependsOn(configurations.appServerDockerDir)
synthesize.dependsOn(configurations.appUIAssetsDir)
assemble.dependsOn(synthesize)

task deploy {
  description """Synthesizes and deploys AWS resources.
    Bypasses approvals for security-related changes."""
  inputs.files(configurations.appServerDockerDir)
  doLast {
    exec {
      workingDir '.'
      // Deploy also synthesizes, regardless of if we've already done a synthesis
      commandLine 'cdk', 'deploy', '--all',
        '--require-approval', 'never',
        '--context',
        "server-docker-dir=${configurations.appServerDockerDir.resolve()[0].absolutePath}",
        '--context',
        "ui-assets-dir=${configurations.appUIAssetsDir.resolve()[0].absolutePath}"
    }
  }
}
deploy.dependsOn(configurations.appServerDockerDir)
deploy.dependsOn(configurations.appUIAssetsDir)

dependencies {
  // Configures appServerDockerDir to come from the packaging subproject
  appServerDockerDir project(path: ':packaging', configuration: 'appServerDockerDir')
  // Configures appUIAssetsDir to come from the app-ui subproject
  appUIAssetsDir project(path: ':app-ui', configuration: 'assets')
}
